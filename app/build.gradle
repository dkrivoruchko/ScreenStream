apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-parcelize"
apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.crashlytics"

android {
    namespace "info.dvkr.screenstream"
    compileSdkVersion(33)
    buildToolsVersion("33.0.0")

    defaultConfig {
        applicationId = "info.dvkr.screenstream"
        minSdkVersion(21)
        targetSdkVersion(33)
        versionCode = 31004
        versionName = "3.10.4"

        // Don't forget to update xml/locales_config.xml
        resConfigs 'en', 'ar', 'ca', 'de', 'es', 'fa', 'fr-rFR', 'gl', 'hi', 'it', 'ja', 'nl', 'pl', 'pt-rBR', 'ru', 'sk', 'uk', 'zh', 'zh-rTW'

        // https://medium.com/@crafty/no-if-you-do-that-then-you-cant-use-newer-features-on-older-platforms-e-g-fa595333c0a4
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        getByName("debug") {
            applicationIdSuffix ".dev"
            minifyEnabled = false
            shrinkResources = false
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
        getByName("release") {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")

            packagingOptions {
                exclude("DebugProbesKt.bin")
            }
        }
    }

    Properties localProps = new Properties()
    File localProperties = new File(rootProject.rootDir, "local.properties")
    if (localProperties.exists() && localProperties.isFile()) {
        localProperties.withInputStream { localProps.load(it) }
    }
    flavorDimensions("default")
    productFlavors {
        create("firebase") {
            manifestPlaceholders = ["adMobPubId": "${localProps.getProperty("ad.pubId")}"]
            buildConfigField "String", "AD_UNIT_ID_A", localProps.getProperty("ad.unitIdA", "")
            buildConfigField "String", "AD_UNIT_ID_B", localProps.getProperty("ad.unitIdB", "")
            buildConfigField "String", "AD_UNIT_ID_C", localProps.getProperty("ad.unitIdC", "")
        }
        create("firebasefree") {
            manifestPlaceholders = ["adMobPubId": ""]
            firebaseCrashlytics.mappingFileUploadEnabled = false
        }
    }

    buildFeatures.viewBinding = true

    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"

        firebase.java.srcDirs += "src/firebase/kotlin"
        firebaseDebug.java.srcDirs += "src/firebaseDebug/kotlin"
        firebaseRelease.java.srcDirs += "src/firebaseRelease/kotlin"

        firebasefree.java.srcDirs += "src/firebasefree/kotlin"
        firebasefreeDebug.java.srcDirs += "src/firebasefreeDebug/kotlin"
        firebasefreeRelease.java.srcDirs += "src/firebasefreeRelease/kotlin"
    }

    packagingOptions {
        resources {
            excludes += ["META-INF/INDEX.LIST"]
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

}

dependencies {
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.2.2")

    implementation(project(":common"))
    implementation(project(":mjpeg"))

    implementation("androidx.core:core-ktx:1.9.0")
    implementation("androidx.activity:activity-ktx:1.6.1")
    implementation("androidx.fragment:fragment-ktx:1.5.5")
    implementation("androidx.appcompat:appcompat:1.6.0-rc01")
    implementation("androidx.constraintlayout:constraintlayout:2.1.4")
    implementation("androidx.recyclerview:recyclerview:1.2.1")
    implementation("com.google.android.material:material:1.7.0")
    implementation("androidx.window:window:1.0.0")

    implementation("androidx.navigation:navigation-fragment-ktx:2.5.3")
    implementation("androidx.navigation:navigation-ui-ktx:2.5.3")
    implementation("androidx.lifecycle:lifecycle-livedata:2.5.1")
    implementation("androidx.lifecycle:lifecycle-common:2.5.1")

    implementation("com.afollestad.material-dialogs:core:3.3.0")
    implementation("com.afollestad.material-dialogs:color:3.3.0")
    implementation("com.afollestad.material-dialogs:input:3.3.0")
    implementation("com.afollestad.material-dialogs:lifecycle:3.3.0")
    // Temp fix for https://github.com/afollestad/material-dialogs/issues/1825
    implementation(fileTree("libs/bottomsheets-release.aar"))
//    implementation("com.afollestad.material-dialogs:bottomsheets:3.3.0")

    implementation("io.insert-koin:koin-android:3.3.1")
    implementation("com.github.iamironz:binaryprefs:1.0.1") //Migrating to datastore

    firebaseImplementation("com.google.android.play:app-update-ktx:2.0.1")
    firebaseImplementation("com.google.firebase:firebase-analytics:21.2.0")
    firebaseImplementation("com.google.firebase:firebase-crashlytics:18.3.2")
    firebaseImplementation("com.google.android.gms:play-services-ads:21.3.0")

//    debugImplementation("com.squareup.leakcanary:leakcanary-android:2.9.1")
}

project.tasks.all {
    if (name.startsWith("injectCrashlyticsMappingFileIdFirebasefree")) enabled = false
    if (name.startsWith("processFirebasefree") && name.endsWith("GoogleServices")) enabled = false
}